@fixed CursorX = 320.0;
@fixed CursorY = 240.0;

@fixed CursorPrevX = 320.0;
@fixed CursorPrevY = 240.0;

@fixed CursorMotionX = 0;
@fixed CursorMotionY = 0;

@fixed CursorSpeedX = 1.0;
@fixed CursorSpeedY = 1.0;

@fixed CursorAreaWidth = 640.0;
@fixed CursorAreaHeight = 480.0;

@bool CursorWrapX = false;
@bool CursorWrapY = false;
	

function int CursorX(void)
{
	return $CursorX;
}

function int CursorY(void)
{
	return $CursorY;
}

function int CursorDeltaX(void)
{
	if ($CursorWrapX)
		return CursorMotionX();
	return $CursorX - $CursorPrevX;
}
function int CursorDeltaY(void)
{
	if ($CursorWrapY)
		return CursorMotionY();
	return $CursorY - $CursorPrevY;
}

function int CursorMotionX(void)
{
	return $CursorMotionX;
}

function int CursorMotionY(void)
{
	return $CursorMotionY;
}

function void SetCursorSpeed(int speed)
{
	$CursorSpeedX = speed;
	$CursorSpeedY = speed;
}

function void SetCursorSpeedX(int speed)
{
	$CursorSpeedX = speed;
}

function void SetCursorSpeedY(int speed)
{
	$CursorSpeedY = speed;
}

function void SetCursorPosition(int x, int y)
{
	$CursorX = x;
	$CursorY = y;
}

function void CenterCursor(void)
{
	SetCursorPosition($CursorAreaWidth / 2, $CursorAreaHeight / 2);
}

function void EnableCursorWrap(bool enable)
{
	$CursorWrapX = enable;
	$CursorWrapY = enable;
}

function void EnableCursorWrapX(bool enable)
{
	$CursorWrapX = enable;
}

function void EnableCursorWrapY(bool enable)
{
	$CursorWrapY = enable;
}

function void ACSUtils_ClampCursor(void)
{
	int width = $CursorAreaWidth;
	int height = $CursorAreaHeight;
	
	int borderX = HudBorderXFor(width);
	int borderY = HudBorderYFor(height);
	
	$CursorX += borderX;
	$CursorY += borderY;
	
	width +=  borderX * 2;
	height += borderY * 2;

	if ($CursorWrapX)
		$CursorX = mod($CursorX, width);
	else
		$CursorX = clamp($CursorX, 0, width);
	
	if ($CursorWrapY)
		$CursorY = mod($CursorY, height);
	else
		$CursorY = clamp($CursorY, 0, height);
		
	$CursorX -= borderX;
	$CursorY -= borderY;
}

function void SetCursorArea(int width, int height)
{
	if (width <= 0)
		ProgramError(StrParam(s:"Cursor area width set to ", d:width));
	if (height <= 0)
		ProgramError(StrParam(s:"Cursor area height set to ", d:height));
	
	$CursorX = FixedMul($CursorX, FixedDiv(width, $CursorAreaWidth));
	$CursorY = FixedMul($CursorY, FixedDiv(height, $CursorAreaHeight));
	$CursorPrevX = FixedMul($CursorPrevX, FixedDiv(width, $CursorAreaWidth));
	$CursorPrevY = FixedMul($CursorPrevY, FixedDiv(height, $CursorAreaHeight));
	$CursorMotionX = FixedMul($CursorMotionX, FixedDiv(width, $CursorAreaWidth));
	$CursorMotionY = FixedMul($CursorMotionY, FixedDiv(height, $CursorAreaHeight));

	$CursorAreaWidth = width;
	$CursorAreaHeight = height;
}

function void UpdateCursor(void)
{
	$CursorPrevX = $CursorX;
	$CursorPrevY = $CursorY;

	int sensitivity = a_GetCVarFixed("mouse_sensitivity");
	int speedX = FixedDiv($CursorSpeedX, FixedMul(a_GetCVarFixed("m_yaw"), sensitivity));
	int speedY = FixedDiv($CursorSpeedY, FixedMul(a_GetCVarFixed("m_pitch"), sensitivity));
	
	speedX = FixedMul(speedX * 2, $CursorAreaWidth) / 64000;
	speedY = FixedMul(speedY * 2, $CursorAreaHeight) / 32000;

	int dx = -GetPlayerInput(ConsolePlayerNumber(), INPUT_YAW)   * speedX;
    int dy = -GetPlayerInput(ConsolePlayerNumber(), INPUT_PITCH) * speedY;

	if (GetCVar("invertmouse"))
        dy = -dy;
		
	$CursorMotionX = dx;
	$CursorMotionY = dy;
	$CursorX += dx;
	$CursorY += dy;
	ACSUtils_ClampCursor();
}
